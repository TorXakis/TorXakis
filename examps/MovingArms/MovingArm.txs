{-
TorXakis - Model Based Testing
Copyright (c) 2015-2017 TNO and Radboud University
See LICENSE at root directory of this repository.
-}

CHANDEF Channels ::=
    Up, Down, Stop, Stopped, Min, Max
ENDDEF

PROCDEF stopped [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Up >-> movingUp [ Up, Down, Stop, Stopped, Min, Max ] ( )
    ##
        Down >-> movingDown [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

PROCDEF movingUp [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Stop >-> stoppingUp [ Up, Down, Stop, Stopped, Min, Max ] ( )
    ##
        Max >-> stoppingAtMax [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

PROCDEF stoppingAtMax [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Stop >-> stoppedMax [ Up, Down, Stop, Stopped, Min, Max ] ( )
    ##
        stoppedMax [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

PROCDEF stoppingUp [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Stopped >-> stopped [ Up, Down, Stop, Stopped, Min, Max ] ( )
    ##
        Max >-> stoppedMax [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

PROCDEF stoppedMax [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Down >-> movingDown [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF


PROCDEF movingDown [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Stop >-> stoppingDown [ Up, Down, Stop, Stopped, Min, Max ] ( )
    ##
        Min >-> stoppingAtMin [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

PROCDEF stoppingAtMin [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Stop >-> stoppedMin [ Up, Down, Stop, Stopped, Min, Max ] ( )
    ##
        stoppedMin [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

PROCDEF stoppingDown [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Stopped >-> stopped [ Up, Down, Stop, Stopped, Min, Max ] ( )
    ##
        Min >-> stoppedMin [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

PROCDEF stoppedMin [ Up, Down, Stop, Stopped, Min, Max ] ( ) ::=
        Up >-> movingUp [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

MODELDEF  Model ::=
    CHAN IN     Up, Down, Stop
    CHAN OUT    Stopped, Min, Max
    BEHAVIOUR   stopped [ Up, Down, Stop, Stopped, Min, Max ] ( )
ENDDEF

CNECTDEF  Sut ::=
        CLIENTSOCK

        CHAN OUT  Up                     HOST "localhost"  PORT 9901
        ENCODE    Up                     ->  ! "up"

        CHAN OUT  Down                   HOST "localhost"  PORT 9902
        ENCODE    Down                   ->  ! "down"

        CHAN OUT  Stop                   HOST "localhost"  PORT 9903
        ENCODE    Stop                   ->  ! "stop"

        CHAN IN   Stopped                HOST "localhost"  PORT 9904
        DECODE    Stopped                <-   ? s

        CHAN IN   Min                    HOST "localhost"  PORT 9905
        DECODE    Min                    <-   ? s
        
        CHAN IN   Max                    HOST "localhost"  PORT 9906
        DECODE    Max                    <-   ? s
ENDDEF

CNECTDEF  Sim ::=
        SERVERSOCK

        CHAN IN   Up                   HOST "localhost"  PORT 9901
        DECODE    Up                   <-   ? s

        CHAN IN   Down                 HOST "localhost"  PORT 9902
        DECODE    Down                 <-   ? s

        CHAN IN   Stop                 HOST "localhost"  PORT 9903
        DECODE    Stop                 <-   ? s

        CHAN OUT  Stopped              HOST "localhost"  PORT 9904
        ENCODE    Stopped              ->  ! "stopped"

        CHAN OUT  Min                  HOST "localhost"  PORT 9905
        ENCODE    Min                  ->  ! "min"

        CHAN OUT  Max                  HOST "localhost"  PORT 9906
        ENCODE    Max                  ->  ! "max"
ENDDEF
