{-
TorXakis - Model Based Testing
Copyright (c) 2015-2017 TNO and Radboud University
See LICENSE at root directory of this repository.
-}

TYPEDEF Cmd ::= CmdInfo
              | CmdLoad    {filePath    :: String}
              | CmdStepper {model       :: String}
              | CmdStep    {nrofSteps   :: Int}
ENDDEF

TYPEDEF Response ::= ResponseInfo       {version, buildTime :: String}
                   | ResponseFailure    {failure            :: String}
                   | ResponseSuccess
ENDDEF

CHANDEF ChanDefs   ::=  Cmd :: Cmd; 
                         Response  :: Response
ENDDEF

PROCDEF cmdInfo  [ Cmd :: Cmd ; Response :: Response]  ( expected :: Response) ::=
        Cmd ! CmdInfo
    >-> Response ! expected
    >-> cmdInfo [ Cmd, Response ] ( expected )
ENDDEF

PROCDEF cmdInfoInit  [ Cmd :: Cmd ; Response :: Response]  ( ) ::=
        Cmd ! CmdInfo
    >-> Response ? ri [[ IF isResponseInfo(ri) THEN (strinre (version(ri), REGEX('(UNVERSIONED)|([0-9]+\.[0-9]+\.[0-9]+)'))) /\ (strinre (buildTime(ri), REGEX('[ -~]+')))
                                               ELSE False 
                                               FI]]
    >-> cmdInfo [ Cmd, Response ] ( ri )
ENDDEF

PROCDEF torxakis [ Cmd :: Cmd ; Response :: Response]  ( ) ::=
    cmdInfoInit [Cmd, Response] ()
ENDDEF

MODELDEF Model ::=
        CHAN IN     Cmd
        CHAN OUT    Response

        BEHAVIOUR   torxakis [ Cmd, Response ] ( )
ENDDEF
