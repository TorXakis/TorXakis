{-
TorXakis - Model Based Testing
Copyright (c) 2015-2017 TNO and Radboud University
See LICENSE at root directory of this repository.
-}

CHANDEF Channels ::=
    UpX, DownX, StopX, StoppedX, MinX, MaxX,
    UpY, DownY, StopY, StoppedY, MinY, MaxY,
    UpZ, DownZ, StopZ, StoppedZ, MinZ, MaxZ
ENDDEF
-- ----------------------------------------------------------------------------------------- --


PROCDEF Semaphore [Acquire, Release]() HIT ::=
    Acquire >-> Release >-> Semaphore [Acquire , Release]()
ENDDEF

PROCDEF controlledMovement  [ Acquire, Release, Up, Down, Stopped, Min, Max ] ( ) HIT ::=
    (
            Acquire | Up  >-> (  Release | Stopped >-> EXIT
                              ## Release | Max >-> EXIT
                              )
        ##
            Acquire | Down>-> (  Release | Stopped >-> EXIT
                              ## Release | Min >-> EXIT
                              )
    ) >>> controlledMovement  [ Acquire, Release, Up, Down, Stopped, Min, Max ] ( )
ENDDEF

PROCDEF singleAxisMovement [ UpX, DownX, StoppedX, MinX, MaxX,
                             UpY, DownY, StoppedY, MinY, MaxY,
                             UpZ, DownZ, StoppedZ, MinZ, MaxZ] ( ) HIT ::=
    HIDE [Acquire, Release] IN
        Semaphore [Acquire, Release]()
    |[Acquire, Release]|
        (   controlledMovement [Acquire, Release, UpX, DownX, StoppedX, MinX, MaxX] ()
        ||| controlledMovement [Acquire, Release, UpY, DownY, StoppedY, MinY, MaxY] ()
        ||| controlledMovement [Acquire, Release, UpZ, DownZ, StoppedZ, MinZ, MaxZ] ()
        )
    NI
ENDDEF

PURPDEF  SingleAxisTestPurpose ::=
    CHAN IN     UpX, DownX, StopX, 
                UpY, DownY, StopY,
                UpZ, DownZ, StopZ
    CHAN OUT    StoppedX, MinX, MaxX,
                StoppedY, MinY, MaxY,
                StoppedZ, MinZ, MaxZ
    GOAL singleAxis ::= singleAxisMovement [ UpX, DownX, StoppedX, MinX, MaxX,
                                             UpY, DownY, StoppedY, MinY, MaxY,
                                             UpZ, DownZ, StoppedZ, MinZ, MaxZ] ( )
ENDDEF
